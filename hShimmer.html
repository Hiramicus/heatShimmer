<!DOCTYPE html>
<html>
  <head>
    <meta name="author" content="Hiram Molina"/>
    <meta charset="utf-8" />
    
    <script type="text/javascript" src="webgl-utils.js"></script>
    <script type="text/javascript" src="webgl-debug.js"></script>
    <script type="text/javascript" src="cuon-utils.js"></script>
    <script type="text/javascript" src="cuon-matrix.js"></script>
    <script type="text/javascript" src="hShimmer.js"></script>
    <script type="text/javascript" src="square.js"></script>
    <script type="text/javascript" src="simpleRoom.js"></script>
    <script type="text/javascript" src="utah_teapot.js"></script>
    <script type="text/javascript" src="chair.js"></script>

    <title>Caution, the tea might be hot.</title>
  </head>
	<h1>Heat shimmer simulation</h1>
	<h2>Definition of goal</h2>
	<p>Our goal is to make an object appear as if it were so hot that it
	made the air around it shimmer. More precisely, this means distort the
	portion of the image where the object or the space around it is visible
	so that it appears the light is bending in that region. This bending
	can't be uniform and it can't be completely random: it shrinks and
	expands the image in globs, but their distribution across space and
	change across time appear to be random.</p>
	<h2>Plan of attack</h2>
	<h3>Overall strategy</h3>
	<p>Our overall strategy is to use frame buffer objects to create a
	texture showing where in the scene the heat distortion can occur, then
	apply a post-process effect on the normally-rendered scene only in the
	portions that match up with the texture.</p>
	<h3>Implementation</h3>
	<p>The object that is meant to be distorted is first rendered to an
	offscreen buffer, but it gets rendered differently: any vertex whose
	normal points up is shifted (translated) upwards by a small amount.
	This permits applying effects to an area besides where the object
	normally resides. In addition to that, it renders it completely
	red.</p>
       	<p>The next stage is to apply the heat effect distortion. It
	does this using a water surface as a displacement map, varying it over
	time, and applying this distortion only in locations where the hot area
	shader colored red and no object lies in the foreground.</p>
	<h2>Future goals</h2>
	<p>There are several things that need to change here.  The most
	important effect is to make the distortion fade away gracefully, in two
	different ways: there should be no outline between the space distorted
	and the space that is not, and the overall shape of the distorted area
	needs to become simpler as the distance increases (a teapot should fade
	into a sphere, etc.). I've tried to get this information across through
	the colors, but it seems that I need to pass information to the shader
	concerning the overall geometry of the model in order to modulate this
	color correctly.</p>
	<h3>Notes</h3>
	<p>This demo works best in Firefox.</p>

  <body onload="init()">
    <script id="singleTextureVertexShader" type="x-shader/x-vertex"> 
      // A shader to allow us to draw something that needs only one texture.
      precision mediump float;

      uniform mat4 projectionMatrix;
      uniform mat4 viewMatrix;
      uniform mat4 modelMatrix;
      uniform vec4 lightPosition;

      attribute vec4 vertexPosition;
      attribute vec3 vertexNormal;
      attribute vec2 vertexTexCoord;
      
      varying vec3 fragmentNormal;
      varying vec3 fragmentLight;
      varying vec3 fragmentView;
      varying vec2 fragmentTexCoord;

      void main()
      {
        fragmentTexCoord = vertexTexCoord;
        mat4 modelViewMatrix = viewMatrix*modelMatrix;
        vec4 p = modelViewMatrix*vertexPosition;
        vec4 q = viewMatrix*lightPosition;

        fragmentNormal = normalize(mat3(modelViewMatrix)*vertexNormal);
        fragmentLight  = normalize(vec3(q - p));
        fragmentView   = normalize(vec3(-1.0*p));

        gl_Position = projectionMatrix*viewMatrix*modelMatrix*vertexPosition;
      }
    </script>
    <script id="singleTextureFragmentShader" type="x-shader/x-fragment">
      precision mediump float;
      
      varying vec3 fragmentNormal;
      varying vec3 fragmentLight;
      varying vec3 fragmentView;
      varying vec2 fragmentTexCoord;
      uniform sampler2D diffuseTexture;

      void main()
      {
        vec3 modelColor = vec3(texture2D(diffuseTexture, fragmentTexCoord));
        vec3 lightColor = vec3(0.6, 0.6, 0.6);
        vec3 fragmentColor;

        vec3 n = normalize(fragmentNormal);
        vec3 l = normalize(fragmentLight);
        vec3 v = normalize(fragmentView);
        vec3 h = normalize(l + v);

        float d = max(dot(n, l), 0.0);
        float s = pow(max(dot(n, h), 0.0), 10.0);
        
        fragmentColor = modelColor*lightColor*d + lightColor*s;
        gl_FragColor = vec4(fragmentColor, 1.0);
      }
    </script>
    
    <script id="textureVertexShader" type="x-shader/x-vertex"> 
      // A shader that allows us to draw a model with three textures: color,
      // specular, and normal.
      precision mediump float;

      uniform mat4 projectionMatrix;
      uniform mat4 viewMatrix;
      uniform mat4 modelMatrix;
      uniform vec4 lightPosition;

      attribute vec4 vertexPosition;
      attribute vec3 vertexNormal;
      attribute vec2 vertexTexCoord;
      attribute vec3 vertexTangent;
      
      varying vec3 fragmentLight;
      varying vec3 fragmentView;
      varying vec2 fragmentTexCoord;

      void main()
      {
        fragmentTexCoord = vertexTexCoord;
        mat4 modelViewMatrix = viewMatrix*modelMatrix;
        vec4 p = modelViewMatrix*vertexPosition;
        vec4 l = viewMatrix*lightPosition;

        vec3 t = mat3(modelViewMatrix)*vertexTangent;
        vec3 n = mat3(modelViewMatrix)*vertexNormal;
        vec3 b = cross(n, t);

        mat3 m = mat3(t, b, n);

        fragmentLight  = vec3(l - p)*m;
        fragmentView   = vec3(-1.0*p)*m;

        gl_Position = projectionMatrix*viewMatrix*modelMatrix*vertexPosition;
      }
    </script>
    <script id="textureFragmentShader" type="x-shader/x-fragment">
      precision mediump float;
      
      varying vec3 fragmentLight;
      varying vec3 fragmentView;
      varying vec2 fragmentTexCoord;
      uniform sampler2D diffuseTexture;
      uniform sampler2D specularTexture;
      uniform sampler2D normalTexture;

      void main()
      {
        vec3 diffuseColor = texture2D(diffuseTexture, fragmentTexCoord).rgb;
        vec3 specularColor = texture2D(specularTexture, fragmentTexCoord).rgb;
        vec3 normalColor = texture2D(normalTexture, fragmentTexCoord).rgb;
        vec3 lightColor = vec3(1.0, 1.0, 1.0);
        vec3 fragmentColor;

        vec3 n = normalize(normalColor*2.0 - 1.0);
        vec3 l = normalize(fragmentLight);
        vec3 v = normalize(fragmentView);
        vec3 h = normalize(l + v);

        float d = max(dot(n, l), 0.0);
        float s = pow(max(dot(n, h), 0.0), 10.0);
        
        fragmentColor = diffuseColor*lightColor*d + specularColor*lightColor*s;
        gl_FragColor = vec4(fragmentColor,  1.0);
      }
    </script>
    <script id="plainVertexShader" type="x-shader/x-vertex"> 
      // This is a do-nothing post-process effect shader.
      precision mediump float;

      attribute vec4 vertexPosition;
      attribute vec2 vertexTexCoord;
      
      varying vec2 fragmentTexCoord;

      void main()
      {
        fragmentTexCoord = vertexTexCoord;
        gl_Position = vertexPosition;
      }
    </script>
    <script id="plainFragmentShader" type="x-shader/x-fragment">
      precision mediump float;
     
      varying vec2 fragmentTexCoord;
      uniform sampler2D colorTexture;

      void main()
      {
        gl_FragColor = texture2D(colorTexture, fragmentTexCoord);
      }
    </script>
    <script id="hotAreaVertexShader" type="x-shader/x-vertex"> 
      // This is much like a regular vertex shader, but it shifts any
      // vertices whose normals point up by a small amount in order to
      // make the area above the object shimmer.
      precision mediump float;

      uniform mat4 projectionMatrix;
      uniform mat4 viewMatrix;
      uniform mat4 modelMatrix;
      uniform mat4 normalMatrix;
      uniform float heatVolume;

      attribute vec4 vertexPosition;
      attribute vec3 vertexNormal;
      
      varying vec3 fragmentNormal;
      varying vec4 fragmentColor;

      void main()
      {
        mat4 modelViewMatrix = viewMatrix*modelMatrix;

        vec3 modelNormal = vec3(normalMatrix*vec4(vertexNormal, 0.0));
        normalize(modelNormal);
        fragmentNormal = modelNormal;

        mat4 shiftOut = mat4(1.0, 0.0, 0.0, 0.0,
                             0.0, 1.0, 0.0, 0.0,
                             0.0, 0.0, 1.0, 0.0,
                             0.2*modelNormal.x, 0.2*modelNormal.y, 0.2*modelNormal.z, 1.0);

        mat4 shiftUp  = mat4(1.0, 0.0, 0.0, 0.0,
                             0.0, 1.0, 0.0, 0.0,
                             0.0, 0.0, 1.0, 0.0,
                             0.0, 0.1, 0.0, 1.0);

        gl_Position = projectionMatrix*viewMatrix*shiftUp*shiftOut*modelMatrix*vertexPosition;
        fragmentColor = vec4(1.0, 0.0, 0.0, 1.0);
      }
    </script>
    <script id="hotAreaFragmentShader" type="x-shader/x-fragment">
      precision mediump float;
      
      varying vec3 fragmentNormal;
      varying vec4 fragmentColor;

      void main()
      {

        gl_FragColor = fragmentColor;
      }
    </script>
    <script id="heatFragmentShader" type="x-shader/x-fragment">
      // This shader actually distorts the image in the right location. It uses
      // many textures in order to determine this. It is a post-process shader,
      // so it only needs to render fragments and has access to the colorTexture.

      // There are two steps here. First we test whether the fragment should be
      // displaced, then we calculate the displacement amount and color.

      // The test works by checking if the hotArea shader lit up the section of
      // the buffer, so we sample that texture and check if it's red, the color
      // that shader outputs. If it is, we can proceed with the distortion.
      // The other test is whether there is an object in the foreground that
      // could block the light. To do this we compare the hot area's depth to
      // the scene depth, i.e., the regular frame buffer's depth texture.

      // Calculating the displacement takes advantage of the fact that heat
      // shimmer looks a bit like water. The calculation uses a water surface
      // normal texture to simulate the displacement. It grabs the red and green
      // components and uses them as a displacement factor for the texture
      // coordinates: It samples the background color at that displaced location.

      // Finally, this displacement needs to vary through time. We do this with a
      // shift variable, provided by the JavaScript, that makes it so that each
      // frame, we draw texels from a set amount above the current coordinate.
      // The JavaScript increments this variable each frame, giving the illusion
      // that the distortion is moving upwards.

      precision mediump float;
     
      varying vec2 fragmentTexCoord;
      uniform sampler2D colorTexture;
      uniform sampler2D hotAreaColorTex;
      uniform sampler2D hotAreaDepthTex;
      uniform sampler2D depthTexture;
      uniform sampler2D displaceTexture;   // shift through space
      uniform sampler2D bgColorTex;
      uniform float shift;                 // shift through time
 
      void main()
      {
        float d = 1.0 / 512.0;
        vec4 currentTexel = texture2D(colorTexture, fragmentTexCoord);
        vec4 sceneDepth = texture2D(depthTexture, fragmentTexCoord);
        vec4 hotAreaDepth = texture2D(hotAreaDepthTex, fragmentTexCoord);

        if(texture2D(hotAreaColorTex, fragmentTexCoord).r > 0.0 && currentTexel.a != 0.0)
        {
          vec2 displacement = texture2D(displaceTexture, vec2(fragmentTexCoord.x, fragmentTexCoord.y + d*shift)).rg;
          displacement = vec2(displacement.r*0.015, displacement.g*0.015);
          gl_FragColor = texture2D(bgColorTex, fragmentTexCoord + displacement);
        }
        else
        {
          gl_FragColor = texture2D(colorTexture, fragmentTexCoord);
        }
        if(sceneDepth.r < hotAreaDepth.r)
        {
          gl_FragColor = texture2D(colorTexture, fragmentTexCoord);
        }
      }
    </script>
    <canvas id="webgl" width="500px" height="500px">
      This content requires <a href="http://get.webgl.org/">Webgl</a>.
    </canvas>
    <br>
    <p>Light position controls, from -5 to 5:</p>
    <label for="xSlider">X</label>
    <input type="range" id="xSlider" min="-5" max="5" step="0.05" value="0.0"></input>
    <br>
    <label for="ySlider">Y</label>
    <input type="range" id="ySlider" min="-5" max="5" step="0.05" value="1.0"></input>
    <br>
    <label for="zSlider">Z</label>
    <input type="range" id="zSlider" min="-5" max="5" step="0.05" value="3.0"></input>
    <br>
    <p>Heat distortion toggle:
    <input type="checkbox" id="heatToggle">
    </p>
  </body>
</html>
